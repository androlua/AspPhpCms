<%
'************************************************************
'作者：云端 (精通ASP/VB/PHP/JS/Flash，交流合作可联系本人)
'版权：源代码公开，各种用途均可免费使用。 
'创建：2016-02-29
'联系：QQ313801120  交流群35915100(群里已有几百人)    邮箱313801120@qq.com   个人主页 sharembweb.com
'更多帮助，文档，更新　请加群(35915100)或浏览(sharembweb.com)获得
'*                                    Powered By AspPhpCMS 
'************************************************************
%>
<% 
'ASP和PHP通用函数


'文章相关标签 组  待改进
Function aritcleRelatedTags(relatedTags)
    Dim c, splStr, s, url 
    splStr = Split(relatedTags, ",") 
    For Each s In splStr
        If s <> "" Then
            If c <> "" Then
                c = c & "," 
            End If 
            url = getColumnUrl(s, "name") 
            c = c & "<a href=""" & url & """ rel=""category tag"" class=""ablue"">" & s & "</a>" & vbCrLf 
        End If 
    Next 

    c = "<footer class=""articlefooter"">" & vbCrLf & "标签： " & c & "</footer>" & vbCrLf 
    aritcleRelatedTags = c 
End Function 


'获得随机文章id列表
Function getRandArticleId(addSql, topNumb)
    Dim splStr, s, c, nIndex 
    rs.Open "select * from " & db_PREFIX & "articledetail " & addSql, conn, 1, 1 
    While Not rs.EOF
        If c <> "" Then c = c & "," 
        c = c & rs("id") 
    rs.MoveNext : Wend : rs.Close 
    getRandArticleId = RandomShow(c, ",", 4) 
    splStr = Split(c, ",") : c = "" : nIndex = 0 
    For Each s In splStr
        If c <> "" Then c = c & "," 
        c = c & s 
        nIndex = nIndex + 1 
        If nIndex >= topNumb Then Exit For 
    Next 
    getRandArticleId = c 
End Function 

'上一篇文章 这里面的sortrank(排序)也可以改为id,在引用的时候就要用id
Function upArticle(parentid, lableName, lableValue)
    Dim sql 
    sql = "select * from " & db_PREFIX & "articledetail where parentid=" & parentid & " and " & lableName & "<" & lableValue & " order by " & lableName & " desc" 
    upArticle = handleUpDownArticle("上一篇：", sql) 
End Function 
'下一篇文章
Function downArticle(parentid, lableName, lableValue)
    Dim sql 
    sql = "select * from " & db_PREFIX & "articledetail where parentid=" & parentid & " and " & lableName & ">" & lableValue & " order by " & lableName & " asc" 
    downArticle = handleUpDownArticle("下一篇：", sql) 
End Function 
'处理上下页
Function handleUpDownArticle(lableTitle, sql)
    Dim c, url 
    'call echo("sql",sql)
    rsx.Open sql, conn, 1, 1 
    If Not rsx.EOF Then
        If isMakeHtml = True Then
            url = getRsUrl(rsx("fileName"), rsx("customAUrl"), "/html/detail" & rsx("id")) 
        Else
            url = handleWebUrl("?act=detail&id=" & rsx("id")) 
        End If 
        c = "<a href=""" & url & """>" & lableTitle & rsx("title") & "</a>" 
    Else
        c = lableTitle & "没有" 
    End If : rsx.Close 
    handleUpDownArticle = c 
End Function 
'获得RS网址 配置上一页 下一页
Function getRsUrl(fileName, customAUrl, defaultFileName)
    Dim url 
    '用默认文件名称
    If fileName = "" Then
        fileName = defaultFileName 
    End If 
    '网址
    If fileName <> "" Then
        url = fileName 
        If InStr(LCase(url), ".html") = False And Right(url, 1) <> "/" Then
            url = url & ".html" 
        End If 
    End If 
    If Trim(customAUrl) <> "" Then
        url = Trim(customAUrl) 
    End If 
    If InStr(cfg_flags, "|addwebsite|") > 0 Then
        url = urlAddHttpUrl(cfg_webSiteUrl, url) 
    End If 
    getRsUrl = url 
End Function 

'获得单页url 20160114
Function getOnePageUrl(title)
    Dim url 
    rsx.Open "select * from " & db_PREFIX & "onepage where title='" & title & "'", conn, 1, 1 
    If Not rsx.EOF Then
        If isMakeHtml = True Then
            url = getRsUrl(rsx("fileName"), rsx("customAUrl"), "/page/page" & rsx("id")) 
        Else
            url = handleWebUrl("?act=onepage&id=" & rsx("id")) 
            If rsx("customAUrl") <> "" Then
                url = rsx("customAUrl") 
            End If 
        End If 
    End If : rsx.Close 

    getOnePageUrl = url 
End Function 
'获得文章
Function getArticleUrl(title)
    Dim url 
    rsx.Open "select * from " & db_PREFIX & "articledetail where title='" & title & "'", conn, 1, 1 
    If Not rsx.EOF Then
        If isMakeHtml = True Then
            url = getRsUrl(rsx("fileName"), rsx("customAUrl"), "/detail/" & rsx("id")) 
        Else
            url = handleWebUrl("?act=article&id=" & rsx("id")) 
            If rsx("customAUrl") <> "" Then
                url = rsx("customAUrl") 
            End If 
        End If 
    End If : rsx.Close 

    getArticleUrl = url 
End Function 
'获得栏目url 20160114
Function getColumnUrl(columnNameOrId, sType)
    Dim url, addSql 

    columnNameOrId = replaceGlobleVariable(columnNameOrId)    '处理动作 <a href="{$GetColumnUrl columnname='[$glb_columnName$]' $}" >更多图片</a>

    If sType = "name" Then
        addSql = " where columnname='" & columnNameOrId & "'" 
    Else
        addSql = " where id=" & columnNameOrId & "" 
    End If 
    rsx.Open "select * from " & db_PREFIX & "webcolumn" & addSql, conn, 1, 1 
    If Not rsx.EOF Then
        If isMakeHtml = True Then
            url = getRsUrl(rsx("fileName"), rsx("customAUrl"), "/nav" & rsx("id")) 
        Else
            url = handleWebUrl("?act=nav&columnName=" & rsx("columnname")) 
            If rsx("customAUrl") <> "" Then
                url = rsx("customAUrl") 
            End If 
        End If 
    End If : rsx.Close 

    getColumnUrl = url 
End Function 
'获得栏目名称对应的id
Function getColumnId(columnName)
    columnName = Replace(columnName, "'", "")                                       '注意，这个不能留
    getColumnId = -1 
    rsx.Open "Select * from " & db_PREFIX & "webcolumn where columnName='" & columnName & "'", conn, 1, 1 
    If Not rsx.EOF Then
        getColumnId = rsx("id") 
    End If : rsx.Close 
End Function 


'获得栏目ID对应的名称
Function getColumnName(columnID)
    rsx.Open "Select * from " & db_PREFIX & "webcolumn where id=" & columnID, conn, 1, 1 
    If Not rsx.EOF Then
        getColumnName = rsx("columnName") 
    End If : rsx.Close 
End Function 




'获得栏目ID对应的类型
Function getColumnType(columnID)
    rsx.Open "Select * from " & db_PREFIX & "webcolumn where id=" & columnID, conn, 1, 1 
    If Not rsx.EOF Then
        getColumnType = rsx("columnType") 
    End If : rsx.Close 
End Function 

'获得栏目ID对应的内容
Function getColumnBodyContent(columnID)
    rsx.Open "Select * from " & db_PREFIX & "webcolumn where id=" & columnID, conn, 1, 1 
    If Not rsx.EOF Then
        getColumnBodyContent = rsx("bodyContent") 
    End If : rsx.Close 
End Function 







'网站统计2014
Function webStat(folderPath)
    Dim dateTime, content, splStr 
    Dim thisUrl, goToUrl, caiShu, c, fileName, co, IE, XP 
    goToUrl = Request.ServerVariables("HTTP_REFERER") 
    thisUrl = "http://" & Request.ServerVariables("HTTP_HOST") & Request.ServerVariables("SCRIPT_NAME") 
    caiShu = Request.ServerVariables("QUERY_STRING") 
    If caiShu <> "" Then
        thisUrl = thisUrl & "?" & caiShu 
    End If 
    goToUrl = Request("GoToUrl") 
    thisUrl = Request("ThisUrl") 
    co = Request.QueryString("co") 
    dateTime = Now() 
    content = Request.ServerVariables("HTTP_USER_AGENT") 
    content = Replace(content, "MSIE", "Internet Explorer") 
    content = Replace(content, "NT 5.0", "2000") 
    content = Replace(content, "NT 5.1", "XP") 
    content = Replace(content, "NT 5.2", "2003") 

    splStr = Split(content & ";;;;", ";") 
    IE = splStr(1) 
    XP = Trim(splStr(2)) 
    If Right(XP, 1) = ")" Then XP = Mid(XP, 1, Len(XP) - 1) 
    c = "来访" & goToUrl & vbCrLf 
    c = c & "当前：" & thisUrl & vbCrLf 
    c = c & "时间：" & dateTime & vbCrLf 
    c = c & "IP:" & getIP() & vbCrLf 
    c = c & "IE:" & getBrType("") & vbCrLf 
    c = c & "Cookies=" & co & vbCrLf 
    c = c & "XP=" & XP & vbCrLf 
    c = c & "Screen=" & Request("screen") & vbCrLf                                  '屏幕分辨率
    c = c & "用户信息=" & Request.ServerVariables("HTTP_USER_AGENT") & vbCrLf       '用户信息

    c = c & "-------------------------------------------------" & vbCrLf 
    'c=c & "CaiShu=" & CaiShu & vbcrlf
    fileName = folderPath & format_Time(Now(), 2) & ".txt" 
    Call createAddFile(fileName, c) 
    c = c & vbCrLf & fileName 
    c = Replace(c, vbCrLf, "\n") 
    c = Replace(c, """", "\""") 
    'Response.Write("eval(""var MyWebStat=\""" & C & "\"""")")
    webStat = c 
End Function 


'读模块内容
Function readTemplateModuleStr(filePath, defaultContent, ModuleId)
    Dim startStr, endStr, content, s 
    startStr = "<!--#Module " & ModuleId & " Start#-->" 
    endStr = "<!--#Module " & ModuleId & " End#-->" 
    'FilePath = ReplaceGlobleLable(FilePath)                '替换全部标签        '添加于2014 12 11

    '文件不存在，则追加模板路径 20150616 给VB软件里用
    If checkFile(filePath) = False Then
        filePath = WebTemplate & filePath 
    End If 

    If defaultContent <> "" Then
        content = defaultContent 
    ElseIf checkFile(filePath) = True Then
        content = GetFText(filePath) 
    Else
        content = code                                                               '默认用内容指定内容
    End If 
    'Call Die("显示" & ModuleId & "," & Content)
    'Call Eerr(filepath & checkfile(filepath), Content)
    If InStr(content, startStr) > 0 And InStr(content, endStr) > 0 Then
        readTemplateModuleStr = StrCut(content, startStr, endStr, 2) 
    Else
        readTemplateModuleStr = "模块[" & ModuleId & "]不存在,路径=" & filePath 
    End If 
End Function 
'找模块对应内容
Function findModuleStr(content, valueStr)
    Dim startStr, endStr, YuanStr, replaceStr, i, Block, BlockFile, action 
    For i = 1 To 9
        startStr = "[$读出内容 " : endStr = "$]" 
        If InStr(valueStr, startStr) > 0 And InStr(valueStr, endStr) > 0 Then
            action = StrCut(valueStr, startStr, endStr, 2) 
            Block = RParam(action, "Block") 
            BlockFile = RParam(action, "File") 
            If InStr(vbCrLf & ReadBlockList & vbCrLf, vbCrLf & Block & vbCrLf) = False Then
                ReadBlockList = ReadBlockList & Block & vbCrLf 
            End If 
            '块文件存在 则读出内容
            If BlockFile <> "" Then
                content = GetFText(BlockFile) 
            End If 
            YuanStr = startStr & action & endStr 
            replaceStr = "" 

            startStr = "<R#读出内容" & Block & " start#>" : endStr = "<R#读出内容" & Block & " end#>" 
            If InStr(content, startStr) > 0 And InStr(content, endStr) > 0 Then
                replaceStr = StrCut(content, startStr, endStr, 2) 
            Else
                startStr = "<!--#读出内容" & Block : endStr = "#-->" 
                If InStr(content, startStr) > 0 And InStr(content, endStr) > 0 Then
                    replaceStr = StrCut(content, startStr, endStr, 2) 
                End If 
            End If 
            'Call Eerr(YuanStr,ReplaceStr)
            valueStr = Replace(valueStr, YuanStr, replaceStr) 
        'Call Echo("ValueStr",ValueStr)
        Else
            '没有模块要处理了 则退出
            Exit For 
        End If 
    Next 
    findModuleStr = valueStr 
End Function 

'判断传值是否相等
Function checkFunValue(action, FunName)
    checkFunValue =(Left(action, Len(FunName)) = FunName) 
End Function

%>    

